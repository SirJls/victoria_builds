# Maintainer: jason ryan <jasonwryan@gmail.com>
pkgname=vimb-git
_pkgname=vimb
pkgver=2.11.r5.g822c2b5
pkgrel=1
pkgdesc="Vimb is a lightweight WebKit-based browser"
arch=('i686' 'x86_64')
url="http://fanglingsu.github.io/vimb"
license=('GPL3')
depends=('webkitgtk2')
provides=('vimb')
conflicts=('vimb')
source=('git+https://github.com/fanglingsu/vimb.git' 'vimb_jls.patch')
md5sums=('SKIP'
         '3bbefe2677a401c8c53ab42600fb7509')

pkgver() {
    cd "${_pkgname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${_pkgname}/src"
    patch -p1 < "$srcdir"/vimb_jls.patch
}

build() {
  cd "${_pkgname}"
	make
}

package() {
  cd "${_pkgname}"
	make DESTDIR="$pkgdir" PREFIX="/usr" install
}

# vim:set ts=2 sw=2 et:
