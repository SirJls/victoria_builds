# Maintainer: jason ryan <jasonwryan@gmail.com>

pkgname=vimb-git
_pkgname=vimb
pkgver=2.11.r0.ge3ea1d3
pkgrel=1
pkgdesc="Vimb is a lightweight WebKit-based browser"
arch=('i686' 'x86_64')
url="http://fanglingsu.github.io/vimb"
license=('GPL3')
depends=('webkitgtk2')
provides=('vimb')
conflicts=('vimb')
source=('git+https://github.com/fanglingsu/vimb.git' 'vimb_jls.patch')
md5sums=('SKIP'
         '79a6d67284ff5e2c9c6f37199db93502')

pkgver() {
    cd "${_pkgname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${_pkgname}"
    patch -p1 < "$srcdir/vimb_jls.patch"
}

build() {
    cd "${_pkgname}"
	make
}

package() {
    cd "${_pkgname}"
	make DESTDIR="$pkgdir" PREFIX="/usr" install
}

# vim:set ts=2 sw=2 et:
