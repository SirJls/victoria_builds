# Maintainer: SirJls (Joris Leendert Sparreboom) <mail@sjorssparreboom.nl>

pkgname=i3
pkgver=4.13
pkgrel=1
license=('BSD')
pkgdesc='An improved dynamic tiling window manager for Xorg'
arch=('i686' 'x86_64')
url='http://i3wm.org/'
replaces=('i3' 'i3bar')
groups=('custom')
depends=(
'xcb-util-cursor'
'xcb-util-keysyms'
'xcb-util-wm'
'xcb-util-xrm'
'libev'
'yajl'
'startup-notification'
'pango'
'libxkbcommon-x11'
)
makedepends=( 'bison' 'flex' 'i3status')
optdepends=(
'i3lock: The i3 lock screen.'
'dmenu: Menu launcher for X.'
'perl'
'perl-json-xs'
'perl-anyevent-i3'
)
options=('docs' '!strip')
source=("http://i3wm.org/downloads/${pkgname}-${pkgver}.tar.bz2"
"http://i3wm.org/downloads/${pkgname}-${pkgver}.tar.bz2.asc")
md5sums=('08d17dcf1fde665a15f7d411486546ae'
         'SKIP')
validpgpkeys=('424E14D703E7C6D43D9D6F364E7160ED4AC8EE1D')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc
  cd ${CARCH}-pc-linux-gnu
  MAKEFLAGS="-j1"
  make CPPFLAGS+="-U_FORTIFY_SOURCE"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/${CARCH}-pc-linux-gnu"
  make DESTDIR="${pkgdir}/" install

  mkdir -p "${pkgdir}/usr/share/man/man1/"
  install -m644 ../man/*.1 "${pkgdir}/usr/share/man/man1/"

  install -Dm644 ../LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
