# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=msmtp
pkgname=('msmtp' 'msmtp-mta')
pkgver=1.6.2
pkgrel=1
arch=('i686' 'x86_64')
groups=('modified')
license=('GPL3')
url="http://msmtp.sourceforge.net"
makedepends=('gsasl')
source=(http://download.sourceforge.net/sourceforge/msmtp/${pkgbase}-${pkgver}.tar.xz)
sha1sums=('2d2272d04851b9bc806a77f47344e61840185329')

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --with-ssl=gnutls --with-libsecret=no
  make
  if [[ -n $(expac -Q '%n' texlive-plainextra) ]]; then
      make -C doc html pdf
  fi
}

package_msmtp() {
  pkgdesc="A mini smtp client"
  depends=('gsasl')
  install=msmtp.install

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install 

  if [[ -n $(expac -Q '%n' texlive-plainextra) ]]; then
  make DESTDIR="${pkgdir}" -C doc install-html install-pdf
  fi

# Installing example configs and scripts to /usr/share/doc/msmtp
# as they are not installed by default (Debian and Gentoo do it this way)
  install -d "${pkgdir}/usr/share/doc/msmtp"
  cp -r scripts/{find_alias,msmtpqueue,msmtpq,set_sendmail} "${pkgdir}/usr/share/doc/msmtp/"
  install -D -m644 doc/*.example "${pkgdir}/usr/share/doc/msmtp/"

  install -D -m644 scripts/vim/msmtp.vim "${pkgdir}/usr/share/vim/vimfiles/syntax/msmtp.vim"
}

package_msmtp-mta() {
  pkgdesc="A mini smtp client - the regular MTA"
  depends=('msmtp')
  provides=('smtp-forwarder')
  conflicts=('smtp-forwarder')

  install -d "${pkgdir}"/usr/bin
  ln -s msmtp "${pkgdir}/usr/bin/sendmail"
}
