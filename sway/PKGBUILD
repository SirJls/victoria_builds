# Maintainer: Sjors Sparreboom <mail@sjorssparreboom.nl>
# Contributor: Drew DeVault <sir@cmpwn.com>

pkgname=sway
pkgver=0.12.rc1.50.g8d9a928
pkgrel=1
license=('MIT')
pkgdesc='i3 compatible window manager for Wayland'
makedepends=("git" "cmake" "asciidoc")
arch=("i686" "x86_64")
url='https://github.com/SiCmpwn/sway'
source=(git://github.com/SirCmpwn/$pkgname)
groups=('custom')
sha256sums=('SKIP')
depends=(
  "wlc"
  "wayland"
  "xorg-server-xwayland"
  "xcb-util-image"
  "json-c"
  "pango"
  "cairo"
  "gdk-pixbuf2"
)
optdepends=(
  "rxvt-unicode: Default terminal emulator."
  "dmenu: Default for launching applications."
  "imagemagick: For taking screenshots."
  "ffmpeg: For recording screencasts."
  "i3status: To display system information with a bar."
)

pkgver() {
  cd $pkgname
  git describe --tags --long | sed 's/-/./g'
}

build() {
  cd $pkgname
  cmake \
    -DCMAKE_BUILD_TYPE=Upstream \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
