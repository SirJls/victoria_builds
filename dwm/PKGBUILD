pkgname=dwm
pkgver=6.1.14.gbb3bd6f
pkgrel=1
pkgdesc="A generic menu for X"
url="http://dwm.suckless.org/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libx11' 'libxinerama' 'libxft')
makedepends=('git')
provides=($pkgname)
conflicts=($pkgname)
groups=('custom')
source=(git://git.suckless.org/$pkgname)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags --long | sed 's/-/./g'
}

prepare() {
  cd $pkgname
  # search for our config
  [[ -f ${SRCDEST}/config.h ]] && cp "${SRCDEST}/config.h" .
  # our patchset
  if [[ $(find "${SRCDEST}/patchset" -type f -exec echo true {} \; | wc -l) > 0 ]]; then
    for PATCH in ${SRCDEST}/patchset/*; do
      patch -p1 < $PATCH
    done
  fi
}

build() {
  cd $pkgname
  make \
    X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
}
